plugins {
	id 'java'
	id 'maven-publish'
	id 'java-gradle-plugin'
	id 'eclipse'
}

def ENV = System.getenv()

base {
	archivesName = project.name
}
version = '1.0'
group = project.maven_group

if (!ENV.MAVEN_URL) {
	version += '+local'
}

repositories {
	maven {
		name = 'Fabric'
				url = 'https://maven.fabricmc.net'
	}
	maven {
		name = 'Ornithe'
				url = 'https://maven.ornithemc.net/releases'
	}
	mavenCentral()
}

dependencies {
	implementation gradleApi()

	implementation 'de.undercouch.download:de.undercouch.download.gradle.plugin:5.6.0'
	implementation 'commons-io:commons-io:2.8.0'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.google.guava:guava:33.1.0-jre'
	implementation 'net.ornithemc:stitch:0.15.7'
	implementation 'net.ornithemc:mapping-utils:0.12.1'
	implementation 'net.ornithemc:nester:1.3.1'
	implementation 'net.fabricmc:tiny-remapper:0.9.0'
	implementation 'io.github.gaming32:signature-changer:1.0.0-beta.1'
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

jar {
	manifest {
		attributes 'Implementation-Version': project.version
	}
}

gradlePlugin {
	plugins {
		keratin {
			id = 'keratin'
			implementationClass = 'net.ornithemc.keratin.KeratinGradlePlugin'
		}
	}
}

publishing {
	publications {
		plugin(MavenPublication) { publication ->
			groupId group
			artifactId base.archivesName.get()
			version version

			from components.java
		}
	}
	repositories {
		maven {
			name = 'release'
			if (ENV.MAVEN_URL) {
				url ENV.MAVEN_URL
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
		maven {
			name = "snapshot"
			if (ENV.SNAPSHOTS_URL) {
				url ENV.SNAPSHOTS_URL
				credentials {
					username ENV.SNAPSHOTS_USERNAME
					password ENV.SNAPSHOTS_PASSWORD
				}
			}
		}
	}
}
